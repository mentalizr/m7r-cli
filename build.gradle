plugins {
    id 'java'
}

group 'org.mentalizr'
version = rootProject.file('version').text.trim()

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation 'com.github.arthurpicht:utils-core:1.2.1'
    implementation 'com.github.arthurpicht:utils-io:0.0.1.4'
    implementation 'com.github.arthurpicht:AP-Configuration:0.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test {
    useJUnitPlatform()
}

task copyVersion(type: Copy) {
    from "version"
    into "src/main/resources"
}
build.dependsOn copyVersion

task extractDependencies(type: Copy, dependsOn: jar) {
    from configurations.runtimeClasspath
    into "$buildDir/dependencies"
}

task fatJar(type: Jar, dependsOn: extractDependencies) {
    manifest {
        attributes 'Main-Class': 'org.mentalizr.cli.M7rCli'
        attributes 'Version': project.property("version")
        attributes 'Build' : new Date().format('yyyyMMddHHmmss')
    }
    from {
        file("$buildDir/dependencies").listFiles().collect { zipTree(it) }
    }
    from {
        file("$buildDir/libs").listFiles().collect { zipTree(it) }
    }
    destinationDirectory.set(file("$buildDir/fatjar"))
}


